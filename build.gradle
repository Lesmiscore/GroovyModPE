import java.util.zip.ZipEntry
import java.util.zip.ZipInputStream
import java.util.zip.ZipOutputStream

group 'GroovyModPE'
version '1.0-SNAPSHOT'

buildscript{
    repositories {
        mavenCentral()
    }
    dependencies{
        classpath 'com.google.android.tools:dx:1.7'
    }
}

apply plugin: 'groovy'
apply plugin: 'java'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
    maven{url "https://github.com/nao20010128nao/MavenRepo/raw/master/releases/"}
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.10'
    compile 'com.nao20010128nao:android:18'
    compile 'org.mozilla:rhino:1.7.7.1'
    compile 'com.google.android.tools:dx:1.7'
    compile 'com.google.guava:guava:19.0'

    compile project(':adapter')
}

tasks.withType(GroovyCompile)*.targetCompatibility="1.7"

task("mergeJavascript"){
    dependsOn "dx"
    doLast{
        System.gc() // clean up memory
        def dexBase64=((byte[])dx.destination.bytes).encodeBase64(false).toString()
        def codeBase=new File(rootDir,"codebase.js").text

        def result=codeBase.replace("<DEX BINARY HERE>",dexBase64)
        new File(rootDir,"build/GroovyModPE.js").text=result
    }
}

task("dx"){
    dependsOn "assembleFullJar","downloadDxJar"
    doLast{
        def findJavaBinary={->
            // choose what executable file to use
            def candidates=[
                new File(System.getenv("JAVA_HOME"),"bin/java.exe"),
                new File(System.getenv("JAVA_HOME"),"bin/java"),
                new File(System.getProperty("java.home"),"bin/java.exe"),
                new File(System.getProperty("java.home"),"bin/java"),
                new File("java.exe"),
                new File("java")
            ]
            def executable=null
            for(File f:candidates){
                if(f.exists()&f.canExecute()){
                    executable=f
                    break
                }
            }
            return executable
        }
        def forkJava={List<String> classpath,String mainClass,List<CharSequence> args->
            File executable=findJavaBinary()
            if(executable==null){
                throw new UnsupportedOperationException("No java binary found: did JAVA_HOME set correctly?")
            }
            List<String> command=[]
            command.add(executable.toString())
            command.add("-Xmx2G")
            command.add("-classpath")
            if(classpath==null){
                command.add(System.getProperty("java.class.path"))
            }else{
                command.add(String.join(File.pathSeparator,classpath))
            }
            command.add(mainClass)
            // GString may cause crash so cast all the members into String forcibly
            args.stream().map({it.toString()}).forEach({command.add it})
            return new ProcessBuilder(command).inheritIO().start()
        }
        def dest=new File(buildDir,"dx/output.zip")
        dest.parentFile.mkdirs()
        def proc=forkJava(["$downloadDxJar.destination"],"com.android.dx.command.Main",["--dex","--core-library","--multi-dex","--output=$dest","$assembleFullJar.archivePath"])
        proc.waitFor()
        assert proc.exitValue()==0

        // a hack to do "dx.destination" to get dex file location
        dx.metaClass.getDestination={->dest}
    }
}

task("downloadDxJar"){
    doLast{
        def dest=new File(buildDir,"tmp/dx.jar")
        dest.parentFile.mkdirs()
        dest.bytes=new URL("https://github.com/nao20010128nao/MavenRepo/raw/master/releases/com/google/android/tools/dx/1.12/dx-1.12.jar").bytes

        // a hack to do "downloadDxJar.destination" to get dx jar location
        downloadDxJar.metaClass.getDestination={->dest}
    }
}

task("assembleFullJar",type: Jar){
    dependsOn "copyGeneratedJar"
    archiveName "${project.name}.jar"
    from zipTree(new File(buildDir,"tmp/${project.name}-${version}.jar"))
}

task("copyGeneratedJar",type: Copy){
    dependsOn "gatherClasses"
    def gathered=jar.archivePath
    def tmp=new File(buildDir,"tmp")
    into tmp
    from(gathered.parentFile) {
        include '*.jar'
    }
}

task("gatherClasses"){
    dependsOn "assemble"
    doLast {
        def exclude="^META-INF/.*\\.(?:[SM]F|[RD]SA)\$"

        def archive = jar.archivePath
        def backup = new File("${archive.absolutePath}.bak")
        backup.delete()
        archive.renameTo(backup)
        new ZipOutputStream(new BufferedOutputStream(archive.newOutputStream())).with { zos ->
            zos.method = DEFLATED
            //Merge libraries
            project.configurations.compile*.toURI().each { singleJar ->
                new File(singleJar).withInputStream {
                    new ZipInputStream(it).with { zis ->
                        ZipEntry entry = null
                        while ((entry = zis.nextEntry) != null) {
                            if (entry.name.matches(exclude))
                                continue
                            try {
                                zos.putNextEntry(new ZipEntry(entry.name))
                                zos.write new UnclosableInputStreamWrapper(zis).bytes
                                //System.console().readLine()
                            } catch (e) {
                                //e.printStackTrace()
                            }
                        }
                    }
                }
            }
            //Merge my code
            backup.withInputStream {
                new ZipInputStream(it).with { zis ->
                    ZipEntry entry = null
                    while ((entry = zis.nextEntry) != null) {
                        if (entry.name.matches(exclude))
                            continue
                        try {
                            zos.putNextEntry(new ZipEntry(entry.name))
                            zos.write new UnclosableInputStreamWrapper(zis).bytes
                            //System.console().readLine()
                        } catch (e) {
                            //e.printStackTrace()
                        }
                    }
                }
            }

            zos.finish()
            zos.close()
        }
    }
}

class UnclosableInputStreamWrapper extends FilterInputStream{
    UnclosableInputStreamWrapper(InputStream inputStream) {
        super(inputStream)
    }
    @Override
    void close() throws IOException {
    }
}
